@Metadata.layer: #CORE
annotate view zc_travel
  with 
{
       @UI.facet: [ { id:              'Travel',
                      purpose:         #STANDARD,
                      type:            #IDENTIFICATION_REFERENCE,
                      label:           'Travel',
                      position:        10 },
                    { id:              'Booking',
                      purpose:         #STANDARD,
                      type:            #LINEITEM_REFERENCE,
                      label:           'Booking',
                      position:        20,
                      targetElement:   '_Booking'}]


  @UI: {
      lineItem:       [ { position: 10, importance: #HIGH } ],
      identification: [ { position: 10, label: 'Travel ID' } ] }
  @Search.defaultSearchElement: true
  TravelId;

  @UI: {
      lineItem:       [ { position: 20, importance: #HIGH } ],
      identification: [ { position: 20 } ],
      selectionField: [ { position: 20 } ] }
  @Consumption.valueHelpDefinition: [{ entity : {name: '/DMO/I_Agency', element: 'AgencyID'  } }]
  @Search.defaultSearchElement: true
  AgencyId;

  @UI: {
      lineItem:       [ { position: 30, importance: #HIGH } ],
      identification: [ { position: 30 } ],
      selectionField: [ { position: 30 } ] }
  @Consumption.valueHelpDefinition: [{ entity : {name: '/DMO/I_Customer', element: 'CustomerID'  } }]
  @Search.defaultSearchElement: true
  CustomerId;

  @UI: {
      lineItem:       [ { position: 40, importance: #MEDIUM } ],
      identification: [ { position: 40 } ] }
  BeginDate;

  @UI: {
      lineItem:       [ { position: 41, importance: #MEDIUM } ],
      identification: [ { position: 41 } ] }
  EndDate;


  @UI: {
      identification: [ { position: 42 } ] }
  BookingFee;

  @UI: {
      lineItem:       [ { position: 43, importance: #MEDIUM } ],
      identification: [ { position: 43, label: 'Total Price' } ] }
  TotalPrice;

  @Consumption.valueHelpDefinition: [{entity: {name: 'I_Currency', element: 'Currency' }}]
  CurrencyCode;

  @UI: {
      lineItem:       [ { position: 50, importance: #HIGH } ],
      identification: [ { position: 45, label: 'Status' } ],
      selectionField: [{ position: 40 }] }
  @Consumption.valueHelpDefinition: [{ entity: { name: '/DMO/I_Overall_Status_VH', element: 'OverallStatus' }}]
  OverallStatus;

  @UI: {
      identification:[ { position: 46 } ]  }
  Description;

  @UI.hidden: true
  LastChangedAt;
  
}